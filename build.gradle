plugins {
    id 'java'
    id 'eclipse'
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

group = 'dev.gdalia'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "LiquifyPkgs"
        url = "https://gitlab.com/api/v4/groups/15460704/-/packages/maven"
        credentials(HttpHeaderCredentials) {
            name = "Private-Token"
            value = project.findProperty("gitlab.key") ?: System.getenv("CORE_REPO_TOKEN")
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }

    maven {url = 'https://repo.papermc.io/repository/maven-public/'}

}

dependencies {
    implementation 'dev.triumphteam:triumph-gui:3.1.3'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'net.liquifymc.integrations:LiquifyAnvilGUI:1.0-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    setLibsDirName("$rootDir/output")
    setDistsDirName("$rootDir/output")
}